%start parse_regex
%derive<Show> Token
%derive<Show> ParseError
%position<Position>

%token EOF
%token<Char> CHAR
%token<(Char, String)> CHAR_ESCAPE
%token DOT "."
%token DOLLAR "$"
%token LBRACKET "["
%token RBRACKET "]"
%token CARET "^"
%token MINUS "-"
%token STAR "*"
%token PLUS "+"
%token QUESTION "?"
%token<(Int, Int?)> REPEAT
%token LPAREN "("
%token RPAREN ")"
%token BAR "|"
// {{named_regex}}
%token<String> TOKEN 

%%

parse_regex -> (Context) -> Regex
  : alternation { $1 }
  ;

alternation -> (Context) -> Regex
  : concatenation { $1 }
  | concatenation "|" alternation { ctx => @regex.alt($1(ctx), $3(ctx)) }
  ;

concatenation -> (Context) -> Regex
  : repetition { $1 }
  | repetition concatenation { ctx => @regex.seq($1(ctx), $2(ctx)) }
  ;

repetition -> (Context) -> Regex
  : primary { $1 }
  | primary "*" { ctx => @regex.star($1(ctx)) }
  | primary "+" { ctx => @regex.plus($1(ctx)) }
  | primary "?" { ctx => @regex.opt($1(ctx)) }
  | primary REPEAT { ctx => @regex.repeat($1(ctx), min_times=$2.0, max_times?=$2.1) }
  ;

primary -> (Context) -> Regex
  : "(" alternation ")" { ctx => $2(ctx) }
  | TOKEN { ctx => ctx.named_regexes.get($1).unwrap() }
  | character_class { _ctx => @regex.char_class($1) }
  | character { _ctx => @regex.char_class(@char_set.singleton($1)) }
  | "-" { _ctx => @regex.char_class(@char_set.singleton('-')) }
  | "." { _ctx => @regex.char_class(@char_set.any) }
  | "$" { _ctx => @regex.eof }
  ;

character_class -> @char_set.T
  : "[" character_class_inner "]" { $2 }
  | "[" "^" character_class_inner "]" { $3.complement() }
  ;

character_class_inner -> @char_set.T
  : { @char_set.empty }
  | character_class_item character_class_inner { $1.union($2) }
  ;

character_class_item -> @char_set.T
  : character { @char_set.singleton($1) }
  | character "-" character { @char_set.range($1, $3) }
  ;

character -> Char
  : CHAR { $1 }
  | CHAR_ESCAPE { $1.0 }
  ;
